# https://just.systems/man/en/chapter_1.html

# list all available commands
default:
    just --list

# clean up all temporary files
clean:
    find {{ justfile_directory() }} -type d -name '.venv' -exec rm -rf {} +
    find {{ justfile_directory() }} -type d -name '.pytest_cache' -exec rm -rf {} +
    find {{ justfile_directory() }} -type d -name '__pycache__' -exec rm -rf {} +
    find {{ justfile_directory() }} -type d -name '.mypy_cache' -exec rm -rf {} +

# install all required dependencies
configure:
    poetry install --sync

# fix auto-fixable issues
fix:
    poetry run ruff format function_app.py && \
    poetry run ruff check --fix --unsafe-fixes function_app.py

# validate code and configuration
validate:
    poetry check --lock && \
    poetry run ruff format --check function_app.py && \
    poetry run ruff check function_app.py && \
    poetry run mypy function_app.py

container_registry_name := ""
container_image_name := ""
docker_image_tag := `date '+%Y%m%d%H%M%S'`

# build a container image for Azure Function App
build-container-image:
    docker build \
      --platform linux/amd64 \
      --tag {{ container_registry_name }}/{{ container_image_name }}:{{ docker_image_tag }} \
      --tag {{ container_registry_name }}/{{ container_image_name }}:latest \
       .

# deploy a container image for Azure Function App
deploy-container-image: build-container-image
    docker push {{ container_registry_name }}/{{ container_image_name }}:{{ docker_image_tag }} && \
    docker push {{ container_registry_name }}/{{ container_image_name }}:latest

afa := ""
arg := ""
volur_api_address := ""
volur_api_token := ""
acr_password := ""
acr_username := ""

# deploy Azure Functions in Docker
deploy-function-app-docker: deploy-container-image
    az functionapp config container set \
       --image={{ container_registry_name }}/{{ container_image_name }}:{{ docker_image_tag }} \
       --registry-server={{ container_registry_name }} \
       --registry-username={{ acr_username }} \
       --registry-password={{ acr_password }} \
       --name={{ afa }}-docker \
       --resource-group={{ arg }} && \
    az functionapp config appsettings set \
        --name={{ afa }}-docker \
        --resource-group={{ arg }} \
        --settings VOLUR_API_ADDRESS={{ volur_api_address }} VOLUR_API_TOKEN={{ volur_api_token }}

# deploy Azure Functions in Python runtime
deploy-function-app-python:
    poetry export --output=requirements.txt --without-hashes
    func azure functionapp publish {{ afa }}-python --python --build remote
    rm -rf requirements.txt
