# list all available commands
default:
    just --list

# clean up all temporary files
clean:
    find {{ justfile_directory() }} -type d -name '.venv' -exec rm -rf {} +
    find {{ justfile_directory() }} -type d -name '.pytest_cache' -exec rm -rf {} +
    find {{ justfile_directory() }} -type d -name '__pycache__' -exec rm -rf {} +
    find {{ justfile_directory() }} -type d -name '.mypy_cache' -exec rm -rf {} +

# install all required dependencies
configure:
    poetry install --sync

# fix auto-fixable issues
fix:
    poetry run ruff format function_app.py && \
    poetry run ruff check --fix --unsafe-fixes function_app.py

# validate code and configuration
validate:
    poetry check --lock && \
    poetry run ruff format --check function_app.py && \
    poetry run ruff check function_app.py && \
    poetry run mypy function_app.py

acr := ""
container_image_name := "volur-ai-sdk-azure-function-example"
docker_image_tag := `date '+%Y%m%d%H%M%S'`

# build a container image for Azure Function App
build-container-image:
    docker build \
      --platform linux/amd64 \
      --tag {{ acr }}/{{ container_image_name }}:{{ docker_image_tag }} \
      --tag {{ acr }}/{{ container_image_name }}:latest \
       .

# deploy a container image for Azure Function App
deploy-container-image: build-container-image
    docker push {{ acr }}/{{ container_image_name }}:{{ docker_image_tag }} && \
    docker push {{ acr }}/{{ container_image_name }}:latest

afa := ""
arg := ""
volur_api_address := ""
volur_api_token := ""

# deploy Azure Function App
deploy-function-app acr_password=`az acr credential show -n {{ acr }} -o jsonc | jq -r '.passwords[0].value'` acr_username=`az acr credential show -n {{ acr }} -o jsonc | jq -r '.username'`: deploy-container-image
    az functionapp config container set \
       --image={{ acr }}/{{ container_image_name }}:{{ docker_image_tag }} \
       --registry-password={{ acr_password }} \
       --registry-server={{ acr }} \
       --registry-username={{ acr_username }} \
       --name={{ afa }} \
       --resource-group={{ arg }} && \
    az functionapp config appsettings set \
        --name={{ afa }} \
        --resource-group={{ arg }} \
        --settings VOLUR_API_ADDRESS={{ volur_api_address }} VOLUR_API_TOKEN={{ volur_api_token }}
